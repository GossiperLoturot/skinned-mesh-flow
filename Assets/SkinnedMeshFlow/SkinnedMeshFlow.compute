#pragma kernel CSInit
#pragma kernel CSMain
#pragma kernel CSPost

struct Edge
{
    int index0;
    int index1;
};

struct StandardVertex
{
    float3 position;
    float3 normal;
    float4 tangent;
};

int VertexCount;
int EdgeCount;
float4x4 AdjustMatrix;

RWStructuredBuffer<StandardVertex> SrcVertexBuffer;
RWStructuredBuffer<StandardVertex> DstVertexBuffer;
RWStructuredBuffer<Edge> EdgeBuffer;
RWStructuredBuffer<int> DegreeBuffer;

RWStructuredBuffer<int> FixedBuffer;
RWStructuredBuffer<float> FloatBuffer;

int ToFixed(float value) {
    return int(value * 1e5);
}

float ToFloat(int value) {
    return float(value) * 1e-5;
}

[numthreads(1024, 1, 1)]
void CSInit (uint3 id : SV_DispatchThreadID)
{
    if (id.x >= uint(VertexCount)) return;

    FixedBuffer[id.x] = 0;
}

[numthreads(1024, 1, 1)]
void CSMain (uint3 id : SV_DispatchThreadID)
{
    if (id.x >= uint(EdgeCount)) return;

    int index0 = EdgeBuffer[id.x].index0;
    int index1 = EdgeBuffer[id.x].index1;

    float3 src0 = SrcVertexBuffer[index0].position;
    float3 src1 = SrcVertexBuffer[index1].position;

    float3 dst0 = mul(AdjustMatrix, float4(DstVertexBuffer[index0].position, 1.0)).xyz;
    float3 dst1 = mul(AdjustMatrix, float4(DstVertexBuffer[index1].position, 1.0)).xyz;

    float elasticity = length(dst1 - dst0) / length(src1 - src0);
    float influence0 = elasticity / DegreeBuffer[index0];
    float influence1 = elasticity / DegreeBuffer[index1];

    InterlockedAdd(FixedBuffer[index0], ToFixed(influence0));
    InterlockedAdd(FixedBuffer[index1], ToFixed(influence1));
}

[numthreads(1024, 1, 1)]
void CSPost (uint3 id : SV_DispatchThreadID)
{
    if (id.x >= uint(VertexCount)) return;

    FloatBuffer[id.x] = ToFloat(FixedBuffer[id.x]);
}
